---
title: "UCS01 Relay Solidity Integration"
description: "Example showcasing how to create a contract that leverages UCS01 Relay to transfer assets."
---
import Code from '#/components/Code.astro'
import { Tabs, TabItem, FileTree } from '@astrojs/starlight/components'

This example demonstrates how to create a Solidity contract that calls Union's UCS01 Relay to transfer assets.

```sh
UCS01_RELAY=0xD0081080Ae8493cf7340458Eaf4412030df5FEEb
```

<FileTree>
- src
  - Transfer.sol
- script
  - Transfer.s.sol
- remappings.txt
- foundry.toml
</FileTree>

## Transfer USDC on Sepolia

```solidity
// src/Transfer.sol
pragma solidity ^0.8.27;

import {IERC20} from "forge-std/interfaces/IERC20.sol";

struct LocalToken {
  address denom;
  uint128 amount;
}

struct Height {
  uint64 revisionNumber;
  uint64 revisionHeight;
}

interface IRelay {
  function send(
    string calldata sourceChannel,
    bytes calldata receiver,
    LocalToken[] calldata tokens,
    string calldata extension,
    Height calldata timeoutHeight,
    uint64 timeoutTimestamp
  ) external;
}

contract Transfer {
  // https://github.com/unionlabs/union/blob/main/evm/README.md#sepolia
  address public constant relay = 0xD0081080Ae8493cf7340458Eaf4412030df5FEEb;

  // https://github.com/unionlabs/union/blob/main/evm/contracts/apps/ucs/01-relay/Relay.sol#L54-L61
  function transferAsset() public {
    LocalToken[] memory tokens = new LocalToken[](1);
    tokens[0].denom = 0x1c7D4B196Cb0C7B01d743Fbc6116a902379C7238;
    tokens[0].amount = 1000000;

    IERC20(tokens[0].denom).approve(relay, 1000000);

    IRelay(relay).send(
      "channel-90",
      hex"a833B03D8ED1228C4791cBfAb22b3ED57954429F",
      tokens,
      "",
      Height({revisionNumber: 100, revisionHeight: 1000000}),
      0
    );
  }
}
```

## Executing the Contract

We will transfer 1 USDC from Sepolia to a Union wallet. The first transfer we will do against a forked network using `anvil`.

### Solidity Script

```solidity
// script/Transfer.s.sol
pragma solidity ^0.8.27;

import {Script, console} from "forge-std/Script.sol";
import {Transfer} from "../src/Transfer.sol";
import {IERC20} from "forge-std/interfaces/IERC20.sol";

contract TransferScript is Script {
  address public constant USDC = 0x1c7D4B196Cb0C7B01d743Fbc6116a902379C7238;

  function run() public {
    uint256 privateKey = vm.envUint("PRIVATE_KEY");
    vm.startBroadcast(privateKey);
    Transfer transfer = new Transfer();

    IERC20(USDC).transfer(address(transfer), 1500000);

    console.log("transferring");
    transfer.transferAsset();
    console.log("complete");
    vm.stopBroadcast();
  }
}
```

### Running a Forked Network

```sh
# grab a sepolia RPC url from chainlist.org
anvil --fork-url $SEPOLIA_RPC_URL
```

### Funding the Wallet

USDC faucet: [faucet.circle.com](https://faucet.circle.com)

or pick a different token from faucet list here [app.union.build/faucet](https://app.union.build/faucet)

### Running the Script

```sh
export PRIVATE_KEY=<PRIVATE_KEY>

forge script script/Transfer.s.sol:TransferScript \
  --fork-url $SEPOLIA_RPC_URL \
  --broadcast
```

we should see something like this:

```ansi
[⠊] Compiling...
No files changed, compilation skipped
Script ran successfully.

== Logs ==
  transferring
  complete

## Setting up 1 EVM.
==========================
Chain 11155111

Estimated gas price: 492.753087966 gwei
Estimated total gas used for script: 688015
Estimated amount required: 0.33902151581692749 ETH
==========================

##### sepolia
✅  [Success]Hash: 0x38b0fc68c482f75c1ca89e069cf18fea712131fd44c930bee03274804e9fc6b7
Block: 6801346
Paid: 0.042409917351145045 ETH (183391 gas * 231.254081995 gwei)
```
